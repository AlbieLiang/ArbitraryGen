// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'groovy'
//apply plugin: 'maven'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/AlbieLiang/ArbitraryGen'   // 项目的主页
def gitUrl = 'https://github.com/AlbieLiang/ArbitraryGen.git'   // Git仓库的url
group = "cc.suitalk.tools"            // Maven Group ID for the artifact，

// 这个version是library的版本，更新后也需要更改这个值
version = '1.0.0'
archivesBaseName = 'arbitrarygen-plugin'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile files('libs/proguard.jar')
}

// 一定要记得使用交叉编译选项，因为我们可能用很高的JDK版本编译，为了让安装了低版本的同学能用上我们写的插件，必须设定source和target
compileGroovy {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    options.encoding = "UTF-8"
}

uploadArchives {
    repositories.mavenDeployer {
        // 如果你公司或者自己搭了nexus私服，那么可以将插件deploy到上面去
        //        repository(url: "http://10.XXX.XXX.XXX:8080/nexus/content/repositories/releases/") {
        //            authentication(userName: "admin", password: "admin")
        //        }
        // 如果没有私服的话，发布到本地也是ok的
        repository(url: "file:${project.rootDir.getAbsolutePath()}/repositories/release")
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                // Add your description here
                name 'The gradle plugin for ArbitraryGen, ArbitraryGen is a multifunction code generator.'     //项目的描述 你可以多写一点
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'albieliang'        //填写的一些基本信息
                        name 'Albie Liang'
                        email 'albieliang@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task packSourcesJar(type: Jar) {
//    from android.sourceSets.main.java.srcDirs
//    from sourceSets.main.allJava
    from sourceSets.main.groovy.srcDirs
    classifier = 'sources'
}

//task createGroovydoc(type: Groovydoc) {
//    source = sourceSets.main.groovy.srcDirs
////    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
////    classpath += project.files(groovy.getBootClasspath().join(File.pathSeparator))
//}

task packGroovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives packGroovydocJar
    archives packSourcesJar
}

Properties properties = new Properties()
//读取properties的配置信息，当然直接把信息写到代码里也是可以的
properties.load(project.rootProject.file('bintray-config.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"          //这个应该是传到maven的仓库的
        name = archivesBaseName    //发布的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}